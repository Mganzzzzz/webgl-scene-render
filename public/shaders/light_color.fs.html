<script type="x-shader/x-fragment" id="light_color.fs">
    // fragment shaders don't have a default precision so we need
    // to pick one. mediump is a good default
    precision mediump float;
    varying vec4 V_WorldPos;
    varying vec3 V_Normal;
    varying vec4 V_texcord;

    uniform sampler2D U_texture;
    uniform vec4 U_AmbientMaterial;
    uniform vec4 U_AmbientLight;

    uniform vec4 U_DiffuseMaterial;
    uniform vec4 U_DiffuseLight;

    uniform vec4 U_SpecularMaterial;
    uniform vec4 U_SpecularLight;
    uniform vec4 U_CameraPos;
    uniform vec4 U_LightPos;
    uniform vec4 U_Setting;

    void main(){
        vec4 texture_color = texture2D(U_texture, V_texcord.xy);
        vec3 light_pos = U_LightPos.xyz;
        vec3 n = normalize(V_Normal.xyz);// 法线
        vec3 OL = light_pos - V_WorldPos.xyz;
        float d = length(OL);// 光源距离当前面的距离
        OL = normalize(OL);

        float constant_factor = 1.0;//设置因子
        float linear_factor = 0.5;
        float quadratic_factor = 0.0;
        float attenuation  = 1.0 / (constant_factor + linear_factor *d + d*d*quadratic_factor);
        float diffuse_intensity = max(dot(n, OL), 0.0);// 漫反射强度
        vec3 speclar_color =vec3(0.0, 0.0, 0.0);
        vec3 diffuse_color = vec3(0.0, 0.0, 0.0);

        if (diffuse_intensity > 0.0) { //如果漫反射的强度大于0 则开始计算镜面反射
            diffuse_color = U_DiffuseMaterial.rgb * U_DiffuseLight.rgb * attenuation * diffuse_intensity;
            if (U_Setting.x==1.0){
                vec3 reflect_l = reflect(-OL, n);// 计算反射光线  reflect函数 通过 入射光线-OL, 法线n 算出反射光线
                vec3 viewDirection =U_CameraPos.xyz - V_WorldPos.xyz;//计算射入到摄像机位置vec3(0.0, 2.0, 5.0)的光线
                viewDirection = normalize(viewDirection);// 单位化
                reflect_l = normalize(reflect_l);
                float shiness  = 10.0;// 反光强度
                //计算出镜面反射
                speclar_color = U_SpecularMaterial.rgb * U_SpecularLight.rgb * pow(max(dot(viewDirection, reflect_l), 0.0), shiness);
            }
        }

        vec3 ambient_color = U_AmbientMaterial.rgb * U_AmbientLight.rgb;
        vec3 final_color = (ambient_color+diffuse_color+speclar_color)* texture_color.rgb;
        gl_FragColor= vec4(final_color, 1.0);
    }

</script>
